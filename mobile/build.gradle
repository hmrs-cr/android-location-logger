apply plugin: 'com.android.application'

def versionMajor = 1
def versionMinor = 1
def verCode = 41
def APP_NAME = "LocationLogger"

def buildTime() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMddHHmms')
    return formattedDate
}

def versionStringBase = "${versionMajor}.${versionMinor}.${verCode}-${buildTime()}"

android {
    compileSdkVersion 27

    defaultConfig {
        applicationId "com.hmsoft." + APP_NAME.toLowerCase()
        minSdkVersion 14
        targetSdkVersion 23
        versionName versionStringBase
        versionCode verCode
    }

    flavorDimensions "tier"

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
        }
    }
    
     productFlavors {
         Frontier {
             versionName versionStringBase + "-frontier"
             buildConfigField "boolean", "DEFAULT_CONFIGURED", "true"
         }


        bhp103 {
            versionName versionStringBase + "-BHP103"
            buildConfigField "boolean", "DEFAULT_CONFIGURED", "true"
        }
        
        genTracker {
            versionName versionStringBase + "-GenTracker"
            buildConfigField "boolean", "DEFAULT_CONFIGURED", "true"
        }
        
        publicEdition {
            versionName versionStringBase + "-Public"
            buildConfigField "boolean", "DEFAULT_CONFIGURED", "false"
        }
     }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            if (variant.buildType.name == "release") {
                outputFileName = APP_NAME + "-${variant.versionName}.apk"
            }
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    implementation 'com.android.support:appcompat-v7:21.0.+'
    /*compile 'com.google.android.gms:play-services:7.5.0'*/
}
