apply plugin: 'com.android.application'

def versionMajor = 1
def versionMinor = 1
def verCode = 18;
def APP_NAME = "LocationLogger"

def buildTime() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMddHHmms')
    return formattedDate
}

def versionStringBase = "${versionMajor}.${versionMinor}.${verCode}-${buildTime()}"

android {
    compileSdkVersion 21
    buildToolsVersion '21.1.2'

    defaultConfig {
        applicationId "com.hmsoft." + APP_NAME.toLowerCase()
        minSdkVersion 9
        targetSdkVersion 21
        versionName versionStringBase
        versionCode verCode
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    
     productFlavors {
        bhp103 {
            versionName versionStringBase + "-BHP103"
            buildConfigField "boolean", "DEFAULT_CONFIGURED", "true"
        }
        
        genTracker {
            versionName versionStringBase + "-GenTracker"
            buildConfigField "boolean", "DEFAULT_CONFIGURED", "true"
        }
        
        publicEdition {
            versionName versionStringBase + "-Public"
            buildConfigField "boolean", "DEFAULT_CONFIGURED", "false"
        }
     }

    applicationVariants.all { variant ->
        if (variant.buildType.name == "release") {
            variant.outputs.each { output ->
                output.outputFile = new File(
                        output.outputFile.parent, APP_NAME + "-${variant.versionName}.apk")
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile "com.android.support:appcompat-v7:21.0.+"
    /*compile 'com.google.android.gms:play-services:7.5.0'*/
}
